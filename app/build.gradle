plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

ext.versionMajor = 1 //Change when making major feature/functions
ext.versionMinor = 3 //Change when making UI updates
ext.versionPatch = 0 //Change when making bug fixes

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.lacolinares.ragemusicph"
        minSdk 22
        targetSdk 31
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.each { output ->
                def appName = "RMPH"
                def date = new Date()
                def formattedDate = date.format("yyyy-MM-dd-kk.mm.ss")
                if(variant.buildType.name.contains('debug')){
                    output.outputFileName = appName + "-${variant.versionName}-${formattedDate}.apk"
                }else{
                    output.outputFileName = appName + "-${variant.versionName}.apk"
                }
            }
    }
}

private Integer generateVersionCode() {
    return versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    return versionName;
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Compose Navigation
    implementation "androidx.navigation:navigation-compose:2.4.2"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.20.3"
    // Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.17.1'
}